<?xml version="1.0" encoding="utf-8"?>
<mx:ComboBox xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="init();">
	
	<mx:Script>
	<![CDATA[
	
		import mx.collections.ArrayCollection;
		import mx.controls.dataGridClasses.DataGridListData;
		/** 値 */
		private var _value:uint;
		public function init():void {
			this.dataProvider		 = new ArrayCollection([
                						{label:'１行テキスト'  ,data:0},
                						{label:'複数行テキスト',data:1},
                						{label:'ファイル指定'  ,data:2},
                						{label:'ページ指定'  ,data:3}
        							]);
			addEventListener(Event.CHANGE, changeHandler, false, 0, true);
		}
		/**
		 * 値の Getter
		 */
		public function get workValue():uint {
			return _value;
		}
		
		/**
		 * 値の Setter
		 * 
		 * @param i_value	セット値
		 */
		public function set workValue(i_value:uint):void {
			_value = i_value;
			invalidateProperties();	
		}
		
		/**
		 * @param i_item	itemRenderer 値
		 */
		public override function set data(i_item:Object):void	{
			super.data = i_item;
			if (this.dataProvider != null) {
				selectedIndex = getLabelIndex( data[DataGridListData(listData).dataField] );
			}
		}

		// ------------------------------------------------------------
		// protected
		// ------------------------------------------------------------
		
		/**
		 * プロパティの更新
		 */
		protected override function commitProperties():void {
			selectedIndex = getLabelIndex( data[DataGridListData(listData).dataField] );
			super.commitProperties();
		}
		
		// ------------------------------------------------------------
		// private
		// ------------------------------------------------------------
		
		/**
		 * 引数値と合うインデックスを取得
		 * 
		 * @param i_value	dataProvider の値
		 * @return selectedIndex 値
		 */
		private function getLabelIndex(i_value:int):uint {
			var arrayCollection:ArrayCollection = dataProvider as ArrayCollection;
			for (var i:uint = 0; i < arrayCollection.length; i++) {
				if (arrayCollection.getItemAt(i).data == i_value) {
					return i;
				}
			}
			return 0;
		}
		
		/**
		 * CombokBox の change イベント
		 * 
		 * @param event	Event
		 */
		private function changeHandler(event:Event):void {
			var arrayCollection:ArrayCollection = dataProvider as ArrayCollection;
			if (listData) {
				workValue = arrayCollection.getItemAt(selectedIndex).data;
				data[DataGridListData(listData).dataField] = workValue;
			}
		}

	]]>
	</mx:Script>

</mx:ComboBox>
